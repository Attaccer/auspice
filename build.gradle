plugins {
    id 'java'
    id("org.jetbrains.kotlin.jvm") version "2.0.20-Beta2"
    id("com.github.johnrengelman.shadow") version "8.1.1"
}

group = 'top.mckingdom.auspice'
version = '0.1-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        name = "spigotmc-repo"
        url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
}

dependencies {
    compileOnly("org.spigotmc:spigot:1.20.6-R0.1-SNAPSHOT")
    compileOnly("org.checkerframework:checker-qual:3.46.0")
    compileOnly("org.yaml:snakeyaml:2.2")
    compileOnly("com.google.guava:guava:31.1-jre")
    compileOnly(fileTree("libs"))
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

tasks {

    build {
        dependsOn("shadowJar")
    }


    shadowJar {

        enabled true

        archiveBaseName = "auspice"
        archiveClassifier = null

        relocate("kotlin", "top.auspice.libs.kotlin")
        relocate("org.yaml.snakeyaml", "top.auspice.libs.snakeyaml")

        exclude(
//                "kotlin-stdlib-2.0.20-Beta2.jar",
                "annotations-13.0.jar",
                "checker-qual-3.46.0.jar",
                "snakeyaml-2.3.jar"
        )
    }

    jar {
        enabled = false
    }

}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
